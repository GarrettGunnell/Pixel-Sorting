#include "PixelSorting.cginc"

#pragma kernel CS_CreateMask
#pragma kernel CS_TestSelectionSort
#pragma kernel CS_TestBitonicSort
#pragma kernel CS_TestCustomSort
#pragma kernel CS_ClearBuffer
#pragma kernel CS_IdentifySpans
#pragma kernel CS_VisualizeSpans
#pragma kernel CS_RGBtoHSL
#pragma kernel CS_PixelSort
#pragma kernel CS_HSLtoRGB
#pragma kernel CS_Composite

half _LowThreshold, _HighThreshold;

uint _BufferWidth, _BufferHeight, _FrameCount;

int _ReverseSorting, _HorizontalSorting, _SortBy;

RWTexture2D<half> _Mask;
RWTexture2D<half4> _ColorBuffer;
RWTexture2D<half4> _SortedBuffer;
RWTexture2D<half4> _HSLBuffer;
RWTexture2D<half> _SpanBuffer;

RWTexture2D<half4> _ClearBuffer;

RWStructuredBuffer<uint> _NumberBuffer;
RWStructuredBuffer<uint> _SortedNumberBuffer;

[numthreads(8,8,1)]
void CS_CreateMask(uint3 id : SV_DispatchThreadID) {
    half4 col = saturate(_ColorBuffer[id.xy]);
    half l = luminance(col.rgb);

    int result = 1;
    if (l < _LowThreshold || _HighThreshold < l)
        result = 0;
    
    _Mask[id.xy] = result;
}

// Selection Sort, so performant!!!!
[numthreads(1,1,1)]
void CS_TestSelectionSort(uint3 id : SV_DISPATCHTHREADID) {
    int n = 16;

    for (int i = 0; i < n - 1; ++i) {
        int minIndex = i;
        for (int j = i + 1; j < n; ++j) {
            if (_NumberBuffer[j] < _NumberBuffer[minIndex])
                minIndex = j;
        }

        int temp = _NumberBuffer[minIndex];
        _NumberBuffer[minIndex] = _NumberBuffer[i];
        _NumberBuffer[i] = temp;
    }
}

//Implementation largely referenced from https://poniesandlight.co.uk/reflect/bitonic_merge_sort/ but without the bugs
#define BITONIC_ELEMENTS 512

groupshared half3 gs_ColorCache[BITONIC_ELEMENTS];

half comparisonValue(half3 col) {
    return luminance(col);
}

void compareAndSwap(int2 idx) {
    half c1 = comparisonValue(gs_ColorCache[idx.x]);
    half c2 = comparisonValue(gs_ColorCache[idx.y]);

    bool result = _ReverseSorting ? c1 >= c2 : c1 <= c2;

    if (result) {
        half3 temp = gs_ColorCache[idx.x];
        gs_ColorCache[idx.x] = gs_ColorCache[idx.y];
        gs_ColorCache[idx.y] = temp;
    }
}

void flip(int h, uint t) {
    int q = ((2 * t) / h) * h;
    uint half_h = h / 2;
    int2 indices = q + int2(t % half_h, h - (t % half_h) - 1);
    compareAndSwap(indices);
}

void disperse(int h, uint t) {
    int q = ((2 * t) / h) * h;
    uint half_h = h / 2;
    int2 indices = q + int2(t % half_h, (t % half_h) + half_h);
    compareAndSwap(indices);
}

[numthreads(BITONIC_ELEMENTS / 2, 1, 1)]
void CS_TestBitonicSort(uint3 id : SV_DISPATCHTHREADID) {
    uint2 t = id.xy;

    if (t.x * 2 + 1 < _BufferWidth) {
        gs_ColorCache[t.x * 2] = _ColorBuffer[uint2(t.x * 2, t.y)].rgb;
        gs_ColorCache[t.x * 2 + 1] = _ColorBuffer[uint2(t.x * 2 + 1, t.y)].rgb;
    } else {
        gs_ColorCache[t.x * 2] = _ReverseSorting ? 1.0f : 0.0f;
        gs_ColorCache[t.x * 2 + 1] = _ReverseSorting ? 1.0f : 0.0f;
    }


    uint n = BITONIC_ELEMENTS;
    
    [unroll]
    for (uint h = 2; h <= n; h *= 2) {
        GroupMemoryBarrierWithGroupSync();
        flip(h, t.x);
        
        [unroll]
        for (uint hh = h / 2; hh > 1; hh /= 2) {
            GroupMemoryBarrierWithGroupSync();
            disperse(hh, t.x);
        }
    }
    
    _SortedBuffer[uint2(t.x * 2, t.y)] = half4(gs_ColorCache[t.x * 2], 1.0f);
    _SortedBuffer[uint2(t.x * 2 + 1, t.y)] = half4(gs_ColorCache[t.x * 2 + 1], 1.0f);
}

// Jank insertion sort to avoid swaps entirely and reduce texture writes to O(n)
[numthreads(1, 1, 1)]
void CS_TestCustomSort(uint3 id : SV_DISPATCHTHREADID) {
    uint minNumber = 10000;
    uint minIndex = 0;

    for (int i = 0; i < 16; ++i) {
        for (int j = 0; j < 16; ++j) {
            uint n = _NumberBuffer[j];

            if (n < minNumber) {
                minIndex = j;
                minNumber = n;
            }
        }

        _SortedNumberBuffer[i] = minNumber;
        _NumberBuffer[minIndex] = 100000;
        minNumber = 10000;
    }
}

[numthreads(8,8,1)]
void CS_ClearBuffer(uint3 id : SV_DISPATCHTHREADID) {
    _ClearBuffer[id.xy] = 0;
}

[numthreads(1, 1, 1)]
void CS_IdentifySpans(uint3 id : SV_DISPATCHTHREADID) {
    uint2 idx = 0;
    uint pos = 0;
    int spanStartIndex = 0;
    int spanLength = 0;

    uint spanLimit = _HorizontalSorting ? _BufferWidth : _BufferHeight;

    while (pos < spanLimit) {
        idx = _HorizontalSorting ? uint2(pos, id.y) : uint2(id.x, pos);

        int mask = _Mask[idx];
        pos++;

        if (mask != 0) {
            spanLength++;
        } else {
            idx = _HorizontalSorting ? uint2(spanStartIndex, id.y) : uint2(id.x, spanStartIndex);
            _SpanBuffer[idx] = spanLength;
            spanStartIndex = pos;
            spanLength = 0;
        }
    }

    if (spanLength != 0) {
        idx = _HorizontalSorting ? uint2(spanStartIndex, id.y) : uint2(id.x, spanStartIndex);
        _SpanBuffer[idx] = spanLength;
    }
}

[numthreads(8, 8, 1)]
void CS_VisualizeSpans(uint3 id : SV_DISPATCHTHREADID) {
    int spanLength = _SpanBuffer[id.xy];

    if (spanLength >= 1) {
        uint seed = id.x + _BufferWidth * id.y + _BufferWidth * _BufferHeight;
        half4 c = half4(hash(seed), hash(seed * 2), hash(seed * 3), 1.0f);

        for (int i = 0; i < spanLength; ++i) {
            uint2 idx = _HorizontalSorting ? uint2(id.x + i, id.y) : uint2(id.x, id.y + i);
            _SortedBuffer[idx] = c;
        }
    }
}

[numthreads(8, 8, 1)]
void CS_RGBtoHSL(uint3 id : SV_DISPATCHTHREADID) {
    _HSLBuffer[id.xy] = half4(RGBtoHSL(_ColorBuffer[id.xy].rgb), 1.0f);
}

half sortMethod(half3 c) {
    if (_SortBy == 0)
        return c.b;
    else if (_SortBy == 1)
        return c.g;
    else if (_SortBy == 2)
        return c.r;
    else
        return c.r + c.g + c.b;
}

groupshared half3 gs_PixelSortCache[1080];

[numthreads(1, 1, 1)]
void CS_PixelSort(uint3 id : SV_DISPATCHTHREADID) {
    int spanLength = _SpanBuffer[id.xy];

    if (spanLength >= 1) {
        half3 currColor = 0.0f;
        half currValue = 10000;
        uint minIndex = 0;
        
        uint2 idx = 0;

        for (int k = 0; k < spanLength; ++k) {
            idx = _HorizontalSorting ? uint2(id.x + k, id.y) : uint2(id.x, id.y + k);
            gs_PixelSortCache[k] = _HSLBuffer[idx].rgb;
        }

        for (int i = 0; i < spanLength; ++i) {
            for (int j = 0; j < spanLength; ++j) {
                half3 c = gs_PixelSortCache[j].rgb;

                half l = sortMethod(c);

                if (l < currValue) {
                    currValue = l;
                    currColor = c;
                    minIndex = j;
                }
            }

            idx = _HorizontalSorting ? uint2(id.x + i, id.y) : uint2(id.x, id.y + i);

            if (_ReverseSorting)
                idx = _HorizontalSorting ? uint2(id.x + (spanLength - i - 1), id.y) : uint2(id.x, id.y + (spanLength - i - 1));

            _SortedBuffer[idx] = half4(currColor, 1.0f);
            gs_PixelSortCache[minIndex] = 100000;
            currValue = 10000;
        }
    }
}

[numthreads(8, 8, 1)]
void CS_HSLtoRGB(uint3 id : SV_DISPATCHTHREADID) {
    _SortedBuffer[id.xy] = half4(HSLtoRGB(_SortedBuffer[id.xy].rgb), 1.0f);
}

[numthreads(8, 8, 1)]
void CS_Composite(uint3 id : SV_DISPATCHTHREADID) {
    if (_Mask[id.xy] == 0) {
        _SortedBuffer[id.xy] = _ColorBuffer[id.xy];
    }
}